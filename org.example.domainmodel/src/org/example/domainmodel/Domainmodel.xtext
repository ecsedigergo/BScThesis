grammar org.example.domainmodel.Domainmodel with org.eclipse.xtext.common.Terminals

generate domainmodel "http://www.example.org/domainmodel/Domainmodel"

Domainmodel:
	(elements+=AbstractElement)*;

AbstractElement:
	PackageDec | Type | Import;

PackageDec:
	'package' name=QualifiedName '{'
	(element+=AbstractElement)*
	'}';

Import:
	'import' importedNameSpace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;

QualifiedName:
	ID ('.' ID)*;

Type:
	DataType | Entity;

DataType:
	'datatype' name=ID;

Entity:
	'entity' name=ID ('extends' superType=[Entity|QualifiedName])? '{'
	(features+=Feature)*
	'}';

Feature:
	(many?="many")? name=ID ":" type=[Type|QualifiedName];